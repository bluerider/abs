# Contributor: Wolfgang Bumiller <blub at speed dot at>
# Contributor: Kim Carlb√§cker <kim dot carlbacker at gmail dot com>

pkgbase=nvidia
pkgname=('nvidia' 'nvidia-utils' 'linux-nvidia-utils') # 'linux-nvidia' 'linux-nvidia-utils')
pkgver=325.15
pkgrel=1
arch=('i686' 'x86_64')
license=('NVIDIA')
makedepends=('freebsd-source')
url="http://www.nvidia.com/"
[[ "$CARCH" = "x86_64" ]] && _arch=x86_64
[[ "$CARCH" = "i686"   ]] && _arch=x86
source=("ftp://download.nvidia.com/XFree86/FreeBSD-${_arch}/${pkgver}/NVIDIA-FreeBSD-${_arch}-${pkgver}.tar.gz"
        nvidia-settings.in
       )
md5sums=('68d26cb0a7fbff0d814cdad945bf1e2f'
         '86da362e820aafb226affa8dec13db3d')

build() {
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"
  export CC=clang
  export CPP=clang-cpp
  export CXX=clang++	
  unset CFLAGS LDFLAGS CPPFLAGS CXXFLAGS
  export CFLAGS="-O"

  make
}

package_nvidia() {
 pkgdesc="NVidia graphics card binary drivers"
 install=nvidia.install
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

# Kernel module
  install -dm755 "${pkgdir}/usr/modules"
  install -m555 src/nvidia.ko "${pkgdir}/usr/modules/nvidia.ko"

# X11 driver
  install -dm755 "${pkgdir}/usr/lib/xorg/modules/drivers"
  install -m755 "obj/nvidia_drv.so" "${pkgdir}/usr/lib/xorg/modules/drivers/nvidia_drv.so"

}

package_nvidia-utils() {
 pkgdesc="NVidia binary OpenGL libraries"
 depends=("nvidia=${pkgver}" 'gtk2' 'pango')
 conflicts=('libgl')
 provides=('libgl')
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

# OpenGL Libraries
  install -dm755 "${pkgdir}/usr/lib"
  for i in libGL.so.1 \
           libnvidia-cfg.so.1 \
           libnvidia-glcore.so.1 \
           libnvidia-tls.so.1
  do
    install -m755 "obj/$i" "${pkgdir}/usr/lib/$i"
    ln -sv "$i" "${pkgdir}/usr/lib/${i%%.1}"
  done

# vdpau
  install -dm755 "${pkgdir}/usr/lib/vdpau"
  # libvdpau_trace.so.1 is provided by libvdpau here as well
  for i in libvdpau_nvidia.so.1
  do
    install -m755 "obj/$i" "${pkgdir}/usr/lib/vdpau/$i"
    ln -sv "$i" "${pkgdir}/usr/lib/vdpau/${i%%.1}"
  done

# tools
  install -dm755 "${pkgdir}/usr/bin"
  install -m755 "obj/nvidia-bug-report.sh" "${pkgdir}/usr/bin/"
  install -m755 "obj/nvidia-xconfig"       "${pkgdir}/usr/bin/"

# X11 GLX module
  install -dm755 "${pkgdir}/usr/lib/xorg/modules/extensions"
  install -m755 "obj/libglx.so.1" "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so.1"
  ln -s 'libglx.so.1' "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

# work around nvidia relying on ports-hacks
  install -dm755 "${pkgdir}/usr/lib/nvidia/tools"
  install -m755 "obj/nvidia-settings"          "${pkgdir}/usr/lib/nvidia/tools/nvidia-settings"
  install -m755 "${srcdir}/nvidia-settings.in" "${pkgdir}/usr/bin/nvidia-settings"
  install -dm755 "${pkgdir}/usr/lib/nvidia/tools/lib"
  for i in libgtk-x11-2.0.so libgdk-x11-2.0.so \
           libpangox-1.0.so libpango-1.0.so \
           libgobject-2.0.so libgmodule-2.0.so \
           libglib-2.0.so
  do
    ln -s "/usr/lib/$i" "${pkgdir}/usr/lib/nvidia/tools/lib/${i}.0"
  done
}

package_linux-nvidia-utils() {
 pkgdesc="Linux - NVidia binary OpenGL libraries"
 depends=('linux-base' "nvidia=${pkgver}" 'linux-gtk2' 'linux-pango')
 conflicts=('linux-libgl')
 provides=('linux-libgl')
  cd "${srcdir}/NVIDIA-FreeBSD-${_arch}-${pkgver}/"

# OpenGL & CUDA Libraries
  install -dm755 "${pkgdir}/compat/linux/usr/lib"
  for i in libGL.so.${pkgver} \
  	   libcuda.so.${pkgver} \
           libnvidia-cfg.so.${pkgver} \
           libnvidia-glcore.so.${pkgver} \
           libnvidia-tls.so.${pkgver}
  do
    install -m755 "obj/linux/$i" "${pkgdir}/compat/linux/usr/lib/${i%%.${pkgver}*}.1"
#    ln -sv "$i" "${pkgdir}/compat/linux/usr/lib/${i%%.${pkgver}}.1"
  done

# vdpau
  install -dm755 "${pkgdir}/compat/linux/usr/lib/vdpau"
  # libvdpau{,_trace}.so.${pkgver} is provided here until linux-libvdpau does...
  for i in libvdpau_nvidia.so.${pkgver} \
  	   libvdpau.so.${pkgver} \
  	   libvdpau_trace.so.${pkgver}
  do
    install -m755 "obj/linux/$i" "${pkgdir}/compat/linux/usr/lib/vdpau/${i%%.${pkgver}*}.1"
#    ln -sv "$i" "${pkgdir}/compat/linux/usr/lib/vdpau/${i%%.1}"
  done
}

