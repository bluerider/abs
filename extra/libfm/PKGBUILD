pkgname=libfm
pkgver=1.1.2
pkgrel=1
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
groups=('lxde')
options=('!libtool')
install=libfm.install
depends=('gtk2' 'menu-cache' 'shared-mime-info' 'desktop-file-utils' 'vala')
source=(http://downloads.sourceforge.net/pcmanfm/$pkgname-$pkgver.tar.gz)
md5sums=('c09072e26b9688a7d171843c0da9b33f')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-gnu-ld 
  gmake LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install

  # Temporary fix to FS#32361
  rm $pkgdir/usr/include/libfm
  mv $pkgdir/usr/include/libfm-1.0/ $pkgdir/usr/include/libfm
}
