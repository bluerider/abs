# $Id: PKGBUILD 180579 2013-03-23 23:09:33Z td123 $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.30.1
pkgrel=3
pkgdesc='Graph visualization software'
url='http://www.graphviz.org/'
license=('custom:EPL')
arch=('i686' 'x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
makedepends=('swig' 'guile' 'lua51' 'ocaml' 'perl' 'python2' 'tk' 'qt4')
optdepends=('guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'python2: python bindings'
            'tcl: tcl bindings'
            'qt4: gvedit')
source=("${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz"
        'LICENSE'
	'patch-cmd-gvpr-gvpr.1'
	'patch-cmd-tools-gvpack.1'
	'patch-config_perl.pl'
	'patch-configure'
	'patch-lib-ortho-trapezoid.c')
sha1sums=('96739220c4bbcf1bd3bd52e7111f4e60497185c6'
          'e78277e502c29abb74b9587a5dc951ec392554ea'
          '1135a575606d1549505a1d621b150f23a5504cc8'
          '969abd9690071c37d6a669bca58f484ded52dc14'
          'b9bf8bee56ce789787f65e04a424e06c42beba91'
          'd3f02c409c286ec0302eaf2527b4abbe3ef4979f'
          '4a2c0a650154e67c2450ff4305e1624774635fd8')
options=('!libtool')
install=install

prepare() {
	cd "${pkgname}-${pkgver}"

	for i in patch-cmd-gvpr-gvpr.1 \
		patch-cmd-tools-gvpack.1 \
		patch-config_perl.pl \
		patch-configure \
		patch-lib-ortho-trapezoid.c; do
	patch -p0 -i ${srcdir}/$i
	done

	# install the graph and cgraph api alongside
	gsed 's/@WITH_CGRAPH_FALSE@//g' -i lib/graph/Makefile.in
}

build() {
	cd "${pkgname}-${pkgver}"

	export PYTHON=python2
	export LUA=lua5.1
	export CC=clang
	export CPP=clang-cpp
	export CXX=clang++

	./configure --prefix=/usr --disable-ocaml --enable-r=no --disable-tcl
	make || gsed '/caml_array_length/d' -i tclpkg/gv/gv_ocaml.cpp # swig goes wonky
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# http://www.graphviz.org/License.php
	install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

	install -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/graphviz/LICENSE

	# everything has been built against cgraph, but use graph as default api
	gsed '/#define WITH_CGRAPH 1/d' -i "${pkgdir}"/usr/include/graphviz/types.h
}
