# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Wolfgang Bumiller <blub@speed.at>

pkgname=i3-wm
_pkgsourcename=i3
pkgver=4.6
pkgrel=1
pkgdesc="An improved dynamic tiling window manager"
arch=('i686' 'x86_64')
url="http://i3wm.org/"
license=('BSD')
groups=("i3")
depends=('libxcursor' 'xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl' 'startup-notification' 'pango' 'perl')
optdepends=('dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display systeminformation with a bar.')
options=('docs' '!strip')
source=(http://i3wm.org/downloads/${_pkgsourcename}-${pkgver}.tar.bz2{,.asc})
md5sums=('11901176eea90632384434c371840cfd'
         'SKIP')

backup=('etc/i3/config')

build() {
  cd "$srcdir/$_pkgsourcename-$pkgver"
  gmake -j1
}

package() {
  cd "$srcdir/$_pkgsourcename-$pkgver"

  gmake DESTDIR="$pkgdir/" install
  
  install -dm755 ${pkgdir}/usr/share/man/man1
  for i in i3 \
           i3bar \
           i3-config-wizard \
           i3-input \
           i3-msg \
           i3-migrate-config-to-v4 \
           i3-nagbar \
           i3-dmenu-desktop \
           i3-dump-log \
           i3-sensible-{editor,pager,terminal}
  do
    install -m644 man/${i}.1 "${pkgdir}/usr/share/man/man1/${i}.1"
  done

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  
  gmake clean
}

# vim:set ts=2 sw=2 et:
