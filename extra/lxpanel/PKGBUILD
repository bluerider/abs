pkgname=lxpanel
pkgver=0.5.12
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'menu-cache' 'lxmenu-data' 'libwnck')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'git')
optdepends=('wireless_management: wifimgr')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
	patch-src__plugins__batt__batt.c
	patch-src__plugins__batt__batt_sys.c
	patch-src__plugins__batt__batt_sys.h
	automake.patch)

sha256sums=('11cfe76f6241bbac556e57a6efc079b6e10dceea76a8bdaf4c749afdb81d15ab'
            'b0a3a738db8264072146bbecaf195ec55b66104ba417198c6ed692a163dd02df'
            'ce8114b99e0af89eda91d0dee1d47ae7cb886ccebee3fd616bfaeb47e41b8f93'
            '216f69ae28f541b00e848eb7bbce44897834717001d8170a19d4f08647f62a21'
            'ce5bf608374188da00ce3f0a1c74d1ddebd160555db4119eebd85f64731fe1d1')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -p0 -i $srcdir/patch-src__plugins__batt__batt_sys.h
  patch -p0 -i $srcdir/patch-src__plugins__batt__batt_sys.c
  patch -p0 -i $srcdir/patch-src__plugins__batt__batt.c
  patch -p0 -i ${srcdir}/automake.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver

  unset CC CPP CXX

  sed -i '' -e 's,linux,sys,' \
                 $srcdir/$pkgname-$pkgver/src/plugins/volume/volume-impl.c
  sed -i ''  -e 's,$$(DATADIRNAME)/locale,share/locale,' \
                 $srcdir/$pkgname-$pkgver/po/Makefile.in.in
  ./autogen.sh

  ./configure \
      --sysconfdir=/etc \
      --prefix=/usr \
      --with-plugins=volume,deskno,kbled,xkb,batt \
      --disable-alsa \
      --enable-man \
      --datarootdir=/usr/share
    gmake
}

package() {
  cd $srcdir/$pkgname-$pkgver
  gmake DESTDIR=$pkgdir install
}
