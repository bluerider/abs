# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgname=slang
pkgver=2.2.4
pkgrel=2
pkgdesc="S-Lang is a powerful interpreted language"
arch=('i686' 'x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre' 'zlib')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(ftp://ftp.fu-berlin.de/pub/unix/misc/slang/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2{,.asc})
md5sums=('7fcfd447e378f07dd0c0bae671fe6487'
         '0e37091fe52b4ef609c3cee8eda0e101')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-all
}
