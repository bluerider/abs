# $Id: PKGBUILD 180833 2013-03-26 15:59:12Z bpiotrowski $
# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=mariadb
pkgname=('libmariadbclient' 'mariadb-clients' 'mariadb')
pkgver=5.5.30
pkgrel=6
arch=('i686' 'x86_64')
license=('GPL')
url='http://mariadb.org/'
makedepends=('cmake')
options=('!libtool')
source=(http://mirrors.supportex.net/$pkgbase/$pkgbase-$pkgver/kvm-tarbake-jaunty-x86/$pkgbase-$pkgver.tar.gz
        mariadb-post.sh
        mariadb-tmpfile.conf
	extra-patch-include_my_compare.h
	patch-CMakeLists.txt
	patch-client_CMakeLists.txt
	patch-cmake_os_FreeBSD.cmake
	patch-extra_CMakeLists.txt
	patch-extra_yassl_taocrypt_src_integer.cpp
	patch-include_CMakeLists.txt
	patch-libmysql_CMakeLists.txt
	patch-libservices_CMakeLists.txt
	patch-man_CMakeLists.txt
	patch-mysys_default.c
	patch-regex_CMakeLists.txt
	patch-scripts_CMakeLists.txt
	patch-scripts_mysqld_safe.sh
	patch-sql_CMakeLists.txt
	patch-sql_sys_vars.cc
	patch-support-files_CMakeLists.txt
	init.d-mysqld)
sha256sums=('64dba5f1819f1d4ebf7ed31d74e4106c981a24b88259995deba3734fb7c4635f'
            '368f9fd2454d80eb32abb8f29f703d1cf9553353fb9e1ae4529c4b851cb8c5dd'
            '2af318c52ae0fe5428e8a9245d1b0fc3bc5ce153842d1563329ceb1edfa83ddd'
            '1cefca9c4eafd19f5e0757ea401c6b2bf447256d4f5b37cb2273c5a51089f01b'
            '356546a8332af570cc04cfb4e6ae8fd021dd96778ff073b62ad3a743beb36c68'
            'edd9689ef767cd29900c3c091576087d5fdaf1e56ad7638b7c6352f2dceac139'
            'f478a489be0c76873c44107f348f559677cbb25ff0a140d537acffaa77b5e904'
            '54922c5b57f81d01b5804ab268e5e2e3b5b6303c8488124e6cdb7ea495dda8dc'
            'feee77750946ab43764d25e4a63e5878349fd5f274cbfd32a3a41439b930f7d9'
            'dfe229e644a4cfd43881a8bf3711add73ab72402bd03e1afae4b4d78f5dacbae'
            'cfdb2e3db9579a2a55be19cec75ff3f826a9d422d4432597a8bc67b3dddd21f8'
            'e50ec5f599ee62aa3a6f3de2b7fbafd05ae59070b9e2a0bcca5811f0b7c876da'
            '3f76f436ae66a6f18f098b87897c21fe7ec3b93f8f9296a6d3d90aeaf849c53b'
            '66f4969db48e8999c9bd8a56f14a97b901edaa319056b1f52c66188e7d80cd74'
            'cd255dfc5bd2d3a258226fc2e85543cf2faf47a1d216edeaea4aefa1b92d86b5'
            'f2fb4107db348bb30e08a9e2f4607aa0c362e41c8e482e6669ca73019af95402'
            '4b1c2916bc1d6d2566b9bf046827b1bde4920eb0e55f8451ec70ea78aaf09ae5'
            '3dc69090fa2e6412899a3d34c97d8f4ac6dcf190a1695df5690efa562481e507'
            'd734d194b1722e788f6dff34c8605e067654363268f796be4aadac1a5ced6e56'
            '2974b531aec88b2906bf779e6682da4ce91373232059e8f303918c611b39335d'
            'e5fbc0bdef8df7f0f38a1f052dc96129cbcd708b36f61c7da0fb8d8dac8c82e0')

build() {
  cd $srcdir/$pkgbase-$pkgver

  for patch in  patch-CMakeLists.txt \
	              patch-client_CMakeLists.txt \
	              patch-cmake_os_FreeBSD.cmake \
	              patch-extra_CMakeLists.txt \
	              patch-extra_yassl_taocrypt_src_integer.cpp \
	              patch-man_CMakeLists.txt \
	              patch-mysys_default.c \
	              patch-regex_CMakeLists.txt \
	              patch-scripts_CMakeLists.txt \
	              patch-scripts_mysqld_safe.sh \
	              patch-sql_CMakeLists.txt \
	              patch-sql_sys_vars.cc \
	              patch-support-files_CMakeLists.txt; do
   patch -p0 -i ${srcdir}/$patch
   done

  cd $srcdir
  mkdir build || true
  cd build

  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/mysql \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DENABLED_LOCAL_INFILE=ON \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=/usr/lib/mysql/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_INCLUDEDIR=include/mysql \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_DOCDIR=share/mysql/docs \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=ON \
    -DWITH_ZLIB=system \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=OFF \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DWITH_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITH_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1 \
    -DWITH_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITHOUT_PBXT_STORAGE_ENGINE=1 \
    -DCMAKE_C_FLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
    -DCMAKE_CXX_FLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
    -DWITH_MYSQLD_LDFLAGS="-L/usr/local/lib ${LDFLAGS}"


  make
}

package_libmariadbclient() {
  pkgdesc='MariaDB client libraries'
  conflicts=('libmysqlclient')
  provides=("libmysqlclient=$pkgver")
  
  cd "$srcdir"/build
  for dir in include libmysql libmysqld libservices; do
    make -C $dir DESTDIR="$pkgdir" install
  done

  install -d "$pkgdir"/usr/bin
  install -m755 scripts/mysql_config "$pkgdir"/usr/bin/
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done
}

package_mariadb-clients() {
  pkgdesc='MariaDB client tools'
  depends=('libmariadbclient')
  conflicts=('mysql-clients')
  provides=("mysql-clients=$pkgver")
  
  cd "$srcdir"/build
  make -C client DESTDIR="$pkgdir" install

  # install man pages
  install -d "$pkgdir"/usr/share/man/man1
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 "$srcdir"/$pkgbase-$pkgver/man/$man.1 "$pkgdir"/usr/share/man/man1/$man.1
  done

  # provided by mariadb
  rm "$pkgdir"/usr/bin/{mysql_{plugin,upgrade},mysqltest}
}

package_mariadb() {
  pkgdesc='Fast SQL database server, drop-in replacement for MySQL'
  backup=('etc/mysql/my.cnf')
  install=mariadb.install
  depends=('mariadb-clients')
  conflicts=('mysql' 'mytop')
  provides=("mysql=$pkgver" 'mytop')
  optdepends=('perl-dbd-mysql: mytop dependency'
              'perl-term-readkey: mytop dependency')
  options=('emptydirs')

  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
  
  install -dm755 "$pkgdir"/etc/mysql/
  install -dm755 "${pkgdir}/etc/init.d/"
  install -m644 "${srcdir}/init.d-mysqld" "${pkgdir}/etc/init.d/mysqld" 
  install -m644 "$pkgdir"/usr/share/mysql/my-medium.cnf "$pkgdir"/etc/mysql/my.cnf
  install -m755 "$srcdir"/mariadb-post.sh "$pkgdir"/usr/bin/mysqld-post

  # provided by libmariadbclient
  rm "$pkgdir"/usr/lib/libmysql*
  rm -r "$pkgdir"/usr/include/
  rm -f "$pkgdir"/usr/bin/mysql_config
  #provided in base
  rm -f ${pkgdir}/usr/bin/perror
  rm -f ${pkgdir}/usr/share/man/man1/perror.1
}

