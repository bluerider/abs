# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox
pkgver=23.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'libvpx' 'libevent' 'nss>=3.15.1' 'hunspell' 'sqlite'
          'libnotify' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'mesa' 'imake'
             'xorg-server-xvfb' 'dos2unix' 'libidl2' 'autoconf2.13')
optdepends=('networkmanager: Location detection via available WiFi networks')
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
options=(!emptydirs)
_port_patches=(
	patch-browser-app-nsBrowserApp.cpp
	patch-bug722975
	patch-bug783463
	patch-bug791305
	patch-bug803480
	patch-bug807492
	patch-bug810716
	patch-bug826985
	patch-bug844818
	patch-build-pgo-profileserver.py
	patch-clang32-libc++-visibility-hack
	patch-duckduckgo
	patch-extensions-spellcheck-hunspell-src-mozHunspell.cpp
	patch-ipc-chromium-Makefile.in
	patch-ipc-chromium-src-base-message_pump_libevent.cc
	patch-js-src-methodjit-MethodJIT.cpp
	patch-media-webrtc-trunk-src-system_wrappers-source-spreadsortlib-spreadsort.hpp
	patch-media-webrtc-trunk-testing-gtest-include-gtest-internal-gtest-port.h
	patch-security_manager_ssl_src_nsNSSComponent.cpp
	patch-toolkit_mozapps_installer_packager_mk
	patch-toolkit_xre_Makefile.in
)
source=(https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2
        mozconfig firefox.desktop vendor.js
        firefox-install-dir.patch
        shared-libs.patch
        firefox-20.0.1-fixed-loading-icon.png
        "${_port_patches[@]}"
)

export CC=gcc
export CPP=cpp
export CXX=g++
prepare() {
  cd mozilla-release
  dos2unix media/webrtc/trunk/webrtc/system_wrappers/source/spreadsortlib/spreadsort.hpp

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch
  patch -Np1 -i ../shared-libs.patch

  for i in "${_port_patches[@]}"; do
    msg "Patch $i"
    patch -p0 -i "${srcdir}/$i"
  done
  msg "Patching done"

  msg "sed mozpng"
  sed -i '' -e '/MOZPNG/s/=[0-9]*/=10511/' configure.in
  msg "preparing"

  # Fix PRE_RELEASE_SUFFIX
  gsed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  mkdir "$srcdir/path"

  # WebRTC build tries to execute "python" and expects Python 2
  ln -s /usr/bin/python2 "$srcdir/path/python"

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  gsed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure

#  # Fix tab loading icon (flickers with libpng 1.6)
#  # https://bugzilla.mozilla.org/show_bug.cgi?id=841734
#  cp "$srcdir/firefox-20.0.1-fixed-loading-icon.png" \
#    browser/themes/gnomestripe/tabbrowser/loading.png
}

build() {
  cd mozilla-release

  if [[ -e /usr/lib/libgcov.a ]]; then
  	  echo "--- /usr/lib/libgcov.a exists - the build would fail"
  	  false
  fi

  export PATH="$srcdir/path:$PATH"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export MOZ_PGO=0 MOZ_PROFILE_GENERATE=0

  #export DISPLAY=:99
  #Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  autoconf-2.13
  cd js/src
  autoconf-2.13
  cd ../..

  if ! gmake -f client.mk build MOZ_PGO=0 MOZ_PROFILE_GENERATE=0; then
    kill $!
    return 1
  fi

  kill $! || true
}

package() {
  cd mozilla-release
  gmake -j1 -f client.mk DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/lib/firefox/defaults/preferences/"
  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/preferences/vendor.js"

  for i in 16 22 24 32 48 256; do
      install -dm755 "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/"
      install -m644 browser/branding/official/default$i.png \
        "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done
  install -dm755 "$pkgdir/usr/share/icons/hicolor/64x64/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/128x128/apps"
  install -dm755 "$pkgdir/usr/share/icons/hicolor/210x210/apps"
  install -m644 browser/branding/official/content/icon64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox.png"
  install -m644 browser/branding/official/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox.png"
  install -m644 browser/branding/official/content/about-logo.png \
    "$pkgdir/usr/share/icons/hicolor/210x210/apps/firefox.png"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  #rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
sha256sums=('2d64bd12cfa80c57e4a7f4156c7b4fde725a6173dfe48e2fac12e1927faf563e'
            'a9917fb5aa0e43e70c6c7988b352dd5320dc00f81f0972b14a472af74bda08e4'
            '3e872d07cc5159c9f1ed8945fa19c1bada4acbf38cd818f98d676e9ae5257a73'
            '4b50e9aec03432e21b44d18c4c97b2630bace606b033f7d556c9d3e3eb0f4fa4'
            'ded67e8204bd5e1c0c5771c0d2c84ff80c998e1543711e7cd804cfe29e8dd1b0'
            'e2b4a00d14f4ba69c62b3f9ef9908263fbab179ba8004197cbc67edbd916fdf1'
            '68e3a5b47c6d175cc95b98b069a15205f027cab83af9e075818d38610feb6213'
            '7df536d99f1934dff49729ca187fd388e9169c56ba79a4c8b4997f6b2866cc1f'
            '2ef20c8e044e612d7d2fa718a39eb4affbb8d6627a0c839d5bebd10a51491ed3'
            '0a5a19ee4acfecd04c92125b11c86483e42511bd24b5e4f05e32b3b3185b496e'
            'bef4a48c96419cb9734e3e93764d6a88d597f046d394b8659ca0e1050c16a06a'
            'd121f76ce2afaf74ce74568c207f2d31593649b4202b1a9794f7cd2f7d2cda6b'
            '28582567cdee44579c1f7c195c1e93929217b47182353bed736dbe7a20c3eb4d'
            '3a2ffe7a6196cda780315aa2b654a054b29b75a023b675faf90058da753d6e4f'
            'de6720f57d18078d8015e067861a333dfd505ba4dc0d47862cd335e4ebd47bec'
            'ed53876ea830acd2688a667562ec05d428b1e767be2b6691891ef9643429d674'
            'babab22318ea4b1a60b9053b42bf092b5e583d8c1003de75c294d8bf961e86e8'
            'ded4520bc2e335966757f28819c281fb35b24c41527882b9efbf8fb1a664ce47'
            'c1577e5220f425f816fbf4d1892c82e590b56854f274f2256b2cacfb255d42cf'
            '876799b56c9160005142fb6dfdded0c780df54fe0a8be8df1114582a39f35a1c'
            '89aaa6ac874c95b254e0c4d349cb7cc868a10a8a22eda67a85e4477e85301681'
            '2667776f33cb6bfabf356a209a30d3f02c7651b833f89da1490d10af16f1744f'
            '986869b6424ca7ec87698a743c9f1b4b5d2c2d2cbaa3fe48f00f7711d4a0a42a'
            '34603cd9991098f3f6a3635b24820080df1d5e7dae0a842a8dcf2882d06c3b3c'
            '777a3d49f7cb5eb4f65ae8f929f4d8822d20e7eee44c5ef226b68ea71a6944ee'
            '7c9234684820f7dc0eca3717241423ccd19917f6c7285b442585825557664969'
            'f1c931fe54a2c753c20743bb0b2d2727e135cd1d9511c4bf1281691e36191649'
            'c069725e1dd449e51861ca70dfd8464ed8525453fcc160129508b205adfd65ce')
