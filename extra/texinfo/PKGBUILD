# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=5.1
pkgrel=1
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base-devel')
install=texinfo.install
options=('!emptydirs')
makedepends=('help2man')
_port_patches=(
               'patch-install-info_install-info.c'
               )
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        ${_port_patches[@]})
md5sums=('54e250014fe698fb4832016158747c03'
         'SKIP'
         'fcc53b4cde28fb7f0a302bcfd4b78bc7')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  for _p in ${_port_patches[@]}; do
    patch -Np0 -i ${srcdir}/${_p}
  done
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  LDFLAGS="$LDFLAGS -lintl" \
  ./configure --prefix=/usr/local --without-included-gettext
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # fix this here as it prevents make from trying to rebuild manual pages
  #sed -i i.bak -e 's#A-z#A-Za-z#' ${pkgdir}/usr/local/bin/texi2dvi

  rm -rf "${pkgdir}/usr/local/lib/charset.alias"
}
