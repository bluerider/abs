# Contributor: Wolfgang Bumiller <archbsd at bumiller dot com>
pkgname=pkgdepdb
pkgver=0.1.3
pkgrel=1
_pkgver=0.1.3
pkgdesc="maintain library dependencies of packages in a database"
license=(BSD)
arch=(i686 x86_64)
url=http://github.com/Blub/pkgdepdb
depends=(libarchive pacman)
conflicts=('readpkgelf')
replaces=('readpkgelf')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/Blub/${pkgname}/archive/${_pkgver}.tar.gz")
sha1sums=('bce9ed64fb39a39f080e3b5a4b206df52919c091')

build() {
	cd $pkgname-$_pkgver
	make SYSCONFDIR=/etc ALPM=yes
}

package() {
	cd $pkgname-$_pkgver
	make DESTDIR="${pkgdir}" \
		SYSCONFDIR=/etc \
		PREFIX=/usr \
		install
}
