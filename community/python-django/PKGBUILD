# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Shahar Weiss <sweiss4@gmx.net>
# Contributor: Claudiu Traistaru <claudiu2004@gmail.com>

pkgbase=django
pkgname=('python-django' 'python2-django')
pkgver=1.5.2
pkgrel=2
pkgdesc="A high-level Python Web framework that encourages rapid development and clean design"
arch=('any')
license=('BSD')
url="http://www.djangoproject.com/"
makedepends=('python2' 'python2-setuptools' 'python' 'python-setuptools')
source=("https://www.djangoproject.com/m/releases/${pkgver:0:3}/Django-$pkgver.tar.gz")
md5sums=('26e83e6394a15a86212777d5f61eae86')
sha256sums=('9a4b19adaaa096843425d426ffbeb928e85d861ff9c106527cb747dc67b434da')

build() {
  cd "$srcdir/Django-$pkgver"
  python2 setup.py build
}

package_python-django() {
  depends=('python')
  optdepends=('python-psycopg2: for PostgreSQL backend')
  cd "$srcdir/Django-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/django-admin.py "$pkgdir"/usr/bin/django-admin3.py
  install -dm755 "$pkgdir"/usr/share/bash-completion/completions 
  install -m644 extras/django_bash_completion "$pkgdir"/usr/share/bash-completion/completions/django-admin3.py
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-django() {
  depends=('python2')
  optdepends=('mysql-python: for MySQL backend'
              'python2-psycopg2: for PostgreSQL backend')
  replaces=('django')
  conflicts=('django')
  cd "$srcdir/Django-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  ln -s django-admin.py "$pkgdir"/usr/bin/django-admin2.py
  install -dm755 "$pkgdir"/usr/share/bash-completion/completions
  install -m644 extras/django_bash_completion "$pkgdir"/usr/share/bash-completion/completions/django-admin.py

  ln -s django-admin.py \
    "$pkgdir"/usr/share/bash-completion/completions/manage.py

  find "$pkgdir"/usr/lib/python2.7/site-packages/django/ -name '*.py' | \
    xargs sed -i '' -e 's|#!/usr/bin/env python$|#!/usr/bin/env python2|'
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
