# Contributor: Todd Musall 
# Contributor: dront78 (Ivan)
# Maintainer: Victor Noel
pkgname=('heimdall')
pkgver=1.4.1rc2
pkgrel=1
pkgdesc="Heimdall is a cross-platform open-source utility to flash firmware (aka ROMs) onto Samsung Galaxy S devices."
arch=('i686' 'x86_64')
url="http://www.glassechidna.com.au/products/heimdall/"
license=('MIT')
depends=('qt4') # 'libusb' is provided by world
optdepends=('android-udev: Udev rules to connect Android devices to your linux box')
makedepends=('gcc' 'gmake')
#source=("heimdall-${pkgver}.tar.gz::http://github.com/Benjamin-Dobell/Heimdall/tarball/v${pkgver}"
#source=("heimdall-${pkgver}.tar.gz::http://github.com/Benjamin-Dobell/Heimdall/tarball/e0e84b2f7a28bd188f104f51a845dc4ed8a9b7ea"
source=("heimdall-${pkgver}.tar.gz::http://github.com/Benjamin-Dobell/Heimdall/tarball/9d7008e4ba010162945d985adf560dce7274bc00"
        'heimdall.desktop')

build() {
  cd ${srcdir}/Benjamin-Dobell-Heimdall-*

  # Build libpit which is needed for compiling heimdall
  cd libpit/
  msg "Configuring libpit"
  ./configure --prefix=/usr --target="${CARCH}-unknown-linux"
  # Default makefile removes libpit.1.4.a which is needed by frontend
  #sed -i '/rm -f libpit-1.4.a/d' Makefile
  msg "Building libpit"
  gmake

  # Build heimdall command line tool
  cd ../heimdall/
 
  msg "Configuring Heimdall"
  ./configure --prefix=/usr DEPS_LIBS="-lusb" DEPS_CFLAGS=" " --target="${CARCH}-unknown-linux"
  msg "Building Heimdall"
  gmake

  # Build heimdall GUI front end
  cd ../heimdall-frontend/

  msg "Building heimdall frontend"
  env OUTPUTDIR="/usr/bin" qmake-qt4 heimdall-frontend.pro
  gmake
}

package() {
  cd ${srcdir}/Benjamin-Dobell-Heimdall-*

  # Install heimdall command line tool
  cd heimdall/

  # Prevent make install from trying to reload udev
  # We'll do this the Arch way at package install time
  mv Makefile Makefile.orig
  sed -e 's/sudo service udev restart/echo sudo service udev restart/' <Makefile.orig >Makefile

  gmake DESTDIR="${pkgdir}" install
  # fix slight bug...
  mv "${pkgdir}/usr/bin/x86_64-unknown-linux-heimdall" "${pkgdir}/usr/bin/heimdall"
  rm -rf "${pkgdir}/lib/"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install heimdall GUI front end
  cd ../heimdall-frontend
  # hack to place heimdall-frontend in /usr/bin
  sed -i '' 's|local\/||g' Makefile
  gmake INSTALL_ROOT="${pkgdir}/" install
  install -dm755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/heimdall.desktop" "${pkgdir}/usr/share/applications/heimdall.desktop"
}
md5sums=('3fc64444fb3920db7c407ee601700acd'
         '6c4de9e74c0d9e7ab4d50af21303b78a')
